/*
 * Syrup Pay Token Library
 *
 * Copyright (C) 2015 SK PLANET. ALL Rights Reserved.
 *
 * This library is free software; you can redistribute it and/or
 * modify it under the terms of the GNU Lesser General Public
 * License as published by the Free Software Foundation; either
 * version 2.1 of the License, or (at your option) any later version.
 *
 * This library is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
 * Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public
 * License along with this library; if not, write to the SK PLANET., Bundang-gu, 264,
 * Pangyo-ro The Planet SK planet co., Ltd., Seongnam-si, Gyeonggi-do, Korea
 * or see https://www.syruppay.co.kr/
 */

buildscript {
    repositories {
        jcenter()
        maven { url "http://mvn.skplanet.com/content/groups/public" }
    }
}

configure(allprojects) { project ->
    ext.javaVersion = '1.5'
    ext.projectVersion = '1.2.35'

    version = projectVersion
    group = 'com.skplanet.syruppay'

    apply plugin: 'idea'
    configure(subprojects.findAll { new File(it.projectDir, 'src').directory }) {
        apply plugin: 'java'
    }
}

configure(subprojects) { project ->
    if (project.plugins.hasPlugin('java')) {
        manifest {
            attributes 'provider': 'gradle'
        }
        repositories {
            mavenCentral()
            maven { url "http://mvn.skplanet.com/content/groups/public" }
        }
        dependencies {
            testCompile 'junit:junit:4.11'
        }
    }
}

project(':syruppay-token') {
    apply plugin: 'maven'
    apply plugin: 'signing'

    archivesBaseName = 'syruppay-token'

    jar {
        manifest {
            attributes("Implementation-Title": "Syrup Pay Token Library", "Implementation-Version": version)
        }
    }


    dependencies {
        compile 'org.codehaus.jackson:jackson-mapper-asl:1.9.13'
        compile 'org.codehaus.jackson:jackson-core-asl:1.9.13'
        compile 'joda-time:joda-time:2.6'
        compile 'com.skplanet.syruppay:jose_jdk1.5:0.3.5'
        compile 'com.skplanet.syruppay:jose_jdk1.6:0.2.1'
    }

    javadoc {
        configure(options) {
            charSet = 'UTF-8'
            encoding = 'UTF-8'
            docEncoding = 'UTF-8'
        }
    }

    task javadocJar(type: Jar, dependsOn: javadoc) { // (1)
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    task sourceJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives jar
        archives sourceJar
        archives javadocJar
    }

    signing {
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment {
                    MavenDeployment deployment -> signing.signPom(deployment)
                }
                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom {
                    project {
                        name 'Syrup Pay Token Java Library'
                        packaging 'jar'
                        // optionally artifactId can be defined here
                        description 'This library helps 3rd party developers or merchants to develop with Syrup Pay service.'
                        url 'https://github.com/skplanet/syruppay-java'

                        scm {
                            connection 'scm:git:https://github.com/skplanet/syruppay-java.git'
                            developerConnection 'scm:git:https://github.com/skplanet/syruppay-java.git'
                            url 'https://github.com/skplanet/syruppay-java'
                        }

                        licenses {
                            license {
                                name 'GNU Lesser General Public License, version 2.1'
                                url 'http://www.gnu.org/licenses/old-licenses/lgpl-2.1.html'
                            }
                        }

                        developers {
                            developer {
                                id 'placebo'
                                name 'HyungTae Lim'
                                email 'placebo2me@gmail.com'
                            }
                        }
                    }
                }
            }
        }
    }

    // to skplanet maven repo
//    uploadArchives {
//        repositories {
//            mavenDeployer {
//                repository(url: "http://mvn.skplanet.com/content/repositories/releases")
//                snapshotRepository(url: "http://mvn.skplanet.com/content/repositories/snapshots")
//            }
//        }
//    }
}
